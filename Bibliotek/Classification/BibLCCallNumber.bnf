lc-calln := <lc-calln-base>
          | <lc-calln-base> <lc-calln-shelf>

lc-calln-base := <lc-caption>
               | <lc-caption> '.' <lc-cutter>

lc-calln-shelf := <suffix>
                | <suffix> <space> <lc-special>
                | <work-mark>
                | <space> <work-mark>
                | <space> <work-mark> <space> <lc-special>
                | <space> <lc-special>

lc-caption := <lc-caption-root>
            | <lc-caption-root> <space> <lc-date>
            | <lc-caption-root> <space> <lc-caption-ordinal>
            | <lc-cpation-root> <space> <lc-date> <space> <lc-caption-ordinal>

lc-caption-root := <subclass>
                 | <subclass> <lc-caption-integer>
                 | <subclass> <lc-caption-integer> '.' <lc-caption-decimal>

lc-caption-integer ::= <digit4>

lc-caption-decimal := <digit16>

lc-caption-ordinal := <lc-caption-ordinal-suffix>
                    | <digit16> <space>
                    | <digit16> <lc-caption-ordinal-suffix>
                    | <digit16> <space> <lc-caption-ordinal-suffix>

lc-caption-ordinal-suffix := <suffix>
                           | <suffix> '.'
                           | <suffix> '.' <space>

lc-cutter := <cutter>
           | <cutter> <lc-cutter>
           | <cutter> <space> <lc-date>
           | <cutter> <space> <lc-cutter>
           | <cutter> <space> <lc-date> <space> <lc-cutter>

lc-special := <lc-special-date>
            | <lc-special-date> <work-mark>
            | <lc-special-ordinal>
            | <lc-special-ordinal> <work-mark>
            | <lc-special> <space> <lc-special>

lc-special-date := <lc-date>
                 | <lc-date-range>

lc-date := <lc-date-num>
         | <lc-date-num> <suffix>

lc-date-range-sep := '-' | '/'

lc-date-range := <lc-date-num> <lc-date-range-sep> <digit> <digit>
               | <lc-date-num> <lc-date-range-sep> <digit> <digit> <suffix>

lc-date-num := <digit> <digit> <digit> <digit>

lc-special-ordinal := <lc-special-ordinal-root>
                    | <lc-special-ordinal-root> <lc-special-ordinal>
                    | <lc-special-ordinal-root> <space> <lc-special-ordinal>

lc-special-ordinal-root := <suffix> '.'
                         | <digit16> <suffix> '.'
                         | <digit16> <space> <suffix> '.'

lc-volume := <letter16> '.' <digit16>
           | <letter16> <space> <digit16>
           | <letter16> '.' <space> <digit16>

suffix := <letter>
        | <letter> <letter>
        | <letter> <letter> <letter>

cutter := <letter>
        | <letter> <digit16>

subclass := <letter>
          | <letter> <letter>
          | <letter> <letter> <letter>

work-mark := <upper> <lower>
           | <upper> <lower> <lower>
           | <space> <upper> <lower>
           | <space> <upper> <lower> <lower>

digit16 := <digit4>
         | <digit4> <digit4>
         | <digit4> <digit4> <digit4>
         | <digit4> <digit4> <digit4> <digit4>

digit4 := <digit>
        | <digit> <digit>
        | <digit> <digit> <digit>
        | <digit> <digit> <digit> <digit>

letter16 := <letter4>
          | <letter4> <letter4>
          | <letter4> <letter4> <letter4>
          | <letter4> <letter4> <letter4> <letter4>

letter4 := <letter>
         | <letter> <letter>
         | <letter> <letter> <letter>
         | <letter> <letter> <letter> <letter>

letter := <upper> | <lower>

upper := 'A' | 'B' | 'C' | ... | 'Z'

lower := 'a' | 'b' | 'c' | ... | 'z'

digit := '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

space := ' ' | '\n' | '\t' | <space> <space>
